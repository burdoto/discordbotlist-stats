buildscript {
    repositories.gradlePluginPortal()
    dependencies.classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.1"
}

apply plugin: 'java-library'

group 'de.kaleidox'
version '3'

apply from: 'gradle/vars.gradle'

def exportedProjects = [
        ':core',
        ':discord4j',
        ':javacord',
        ':jda'
]

wrapper {
    gradleVersion = '5.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

allprojects {
    tasks.withType(Javadoc) {
        options {
            encoding = 'UTF-8'
            links = [
                    "https://docs.oracle.com/javase/8/docs/api/",
                    "https://docs.oracle.com/javaee/7/api/",
                    "https://docs.javacord.org/api/v/3.0.4/",
                    "https://www.javadoc.io/doc/com.discord4j/discord4j-core/3.0.9", // broken
                    "https://ci.dv8tion.net/job/JDA/javadoc/" // broken
            ]
        }
    }
}

subprojects {
    apply plugin: 'java-library'

    group 'de.kaleidox'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = "sources"
    }

    task javadocJar(type: Jar) {
        //javadoc
        from javadoc.destinationDir
        archiveClassifier = "javadoc"
    }

    compileJava.options.encoding = 'UTF-8'
    repositories.jcenter()

    dependencies {
        compileOnly 'org.jetbrains:annotations:17.0.0'
        compileOnly 'org.projectlombok:lombok:1.18.10'
        annotationProcessor 'org.projectlombok:lombok:1.18.8'

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.easymock:easymock:4.0.2'

        testCompileOnly 'org.codehaus.groovy:groovy:3.0.0-beta-3'
    }
}

project(':core') {
    version '0.1.2'
}

project(':catnip') {
    version '0.1.0'

    repositories {
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        api project(':core')

        compileOnly 'com.mewna:catnip:1.3.3'
        compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.10.0.pr2'
        compileOnly 'com.squareup.okhttp3:okhttp:3.14.3'
    }
}

project(':discord4j') {
    version '0.1.2'

    dependencies {
        api project(':core')

        compileOnly 'com.discord4j:discord4j-core:3.0.7'
        compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.10.0.pr2'
    }
}

project(':javacord') {
    version '0.1.2'

    dependencies {
        api project(':core')

        compileOnly 'org.javacord:javacord-api:3.0.4'
        compileOnly 'org.javacord:javacord-core:3.0.4'
        compileOnly 'org.apache.logging.log4j:log4j-api:2.12.1'
        compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.10.0.pr2'
        compileOnly 'com.squareup.okhttp3:okhttp:3.14.3'
    }
}

project(':jda') {
    version '0.1.2'

    dependencies {
        api project(':core')

        compileOnly 'net.dv8tion:JDA:3.8.3_463'
        compileOnly 'org.json:json:20190722'
        compileOnly 'com.squareup.okhttp3:okhttp:3.14.3'
    }
}

task allJavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    options.encoding = 'UTF-8'
    options.destinationDirectory(file("./docs/"))
}

apply from: 'gradle/publishing.gradle'
